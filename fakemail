#!/usr/bin/perl
#
# $Id: fakemail,v 1.3 2005/03/08 19:54:02 lastcraft Exp $
#
use Net::Server::Mail::SMTP;
use IO::Socket::INET;
use Getopt::Long;
use POSIX;

# Bail out if missing parameters.
#
my ($host, $port, $path, $background);
GetOptions(
        'host=s'      => \$host,
        'port=i'      => \$port,
        'path=s'      => \$path,
        'background'   => \$background);

if (! defined($host) or ! defined($port) or ! defined($path)) {
    die "Usage: ./fakemail.pl\n" .
            "       --host=<localdomain>\n" .
            "       --port=<port number>\n" .
            "       --path=<path to save mails>\n" .
            "       --background\n";
};
$path =~ s|/$||;

# Run in background.
#
if ($background) {
    my $child_pid = fork;
    die ($!) unless defined ($child_pid);
    if ($child_pid) {
        print "$child_pid";
        exit;
    }
    POSIX::setsid() or die ('Cannot detach from session: $!');
    close(STDIN);
    close(STDOUT);
    close(STDERR);
    $SIG{INT} = $SIG{TERM} = $SIG{HUP} = \&quit;
    $SIG{PIPE} = 'IGNORE';
}
serve();
quit();

# SMTP server.
#
{
    my @local_domains;
    my $server;
    my $socket;

    # Start the server.
    #
    sub serve {
        @local_domains = ($host);
        $server = new IO::Socket::INET Listen => 1, LocalPort => $port;
        while ($socket = $server->accept) {
            my $smtp = new Net::Server::Mail::SMTP socket => $socket;
            $smtp->set_callback(RCPT => \&validate_recipient);
            $smtp->set_callback(DATA => \&queue_message);
            $smtp->process();
            close_socket($socket);
        }
        close_socket($server);
    }

    # Event handlers.
    #
    sub validate_recipient {
        my($session, $recipient) = @_;
        return (1);
    }

    sub queue_message {
        my ($session, $data) = @_;

        my $sender = $session->get_sender();
        my @recipients = $session->get_recipients();
        foreach my $recipient (@recipients) {
            open(FILE, "> " . get_filename_from_recipient($recipient));
            print FILE ${$data};
            close(FILE);
        }
        return (1, 250, "message queued");
    }

    sub quit {
        if (defined($socket)) {
            close_socket($socket);
        }
        close_socket($server);
        exit;
    }
    
    sub close_socket {
        my $socket = shift;
        
        $socket->flush();
        $socket->shutdown();
        $socket->close();
    }
}

# Helpers
#
{
    my %counts = ();

    sub get_filename_from_recipient {
        my $recipient = shift;

        if (! defined($counts{$recipient})) {
            $counts{$recipient} = 1;
        }
        return "$path/$recipient." . $counts{$recipient}++;
    }
}
